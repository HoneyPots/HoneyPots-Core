plugins {
    id 'idea'
    id 'org.springframework.boot' version '2.6.6'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'java'
    id 'distribution'
}

group 'com.honeypot'
version '1.1.1'
sourceCompatibility = '17'

repositories {
    mavenCentral()
}

dependencies {
    // Spring boot
    implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-jdbc'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-webflux'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'

    // Spring cloud
    implementation 'org.springframework.cloud:spring-cloud-starter-aws:2.2.6.RELEASE'

    // Micrometer
    implementation 'io.micrometer:micrometer-core'
    implementation 'io.micrometer:micrometer-registry-prometheus'

    // Querydsl
    implementation 'com.querydsl:querydsl-jpa:5.0.0'
    annotationProcessor(
            "javax.persistence:javax.persistence-api",
            "javax.annotation:javax.annotation-api",
            "com.querydsl:querydsl-apt:5.0.0:jpa"
    )

    // H2
    runtimeOnly 'com.h2database:h2:2.1.212'

    // JWT
    implementation 'io.jsonwebtoken:jjwt-api:0.11.5'
    implementation 'io.jsonwebtoken:jjwt-impl:0.11.5'
    implementation 'io.jsonwebtoken:jjwt-jackson:0.11.5'

    // json-simple
    implementation 'com.googlecode.json-simple:json-simple:1.1.1'

    // fcm
    implementation 'com.google.firebase:firebase-admin:8.2.0'

    // Awaitility
    testImplementation 'org.awaitility:awaitility:4.2.0'

    // Mapstruct & Lombok
    implementation 'org.mapstruct:mapstruct:1.5.1.Final'
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
    annotationProcessor 'org.mapstruct:mapstruct-processor:1.5.1.Final'

    // Mockito
    testImplementation 'org.mockito:mockito-inline:4.6.1'

    // OkHttp & MockWebServer
    testImplementation 'com.squareup.okhttp3:okhttp:4.10.0'
    testImplementation 'com.squareup.okhttp3:mockwebserver:4.10.0'
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

sourceSets {
    main {
        java {
            srcDirs = ["$projectDir/src/main/java", "$projectDir/build/generated"]
        }
    }
}

compileJava {
    options.compilerArgs += [
            '-Amapstruct.suppressGeneratorTimestamp=true',
            '-Amapstruct.suppressGeneratorVersionInfoComment=true',
            '-Amapstruct.verbose=true'
    ]
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

test {
    useJUnitPlatform()
}

tasks.named("jar") {
    enabled = false
}

tasks.named("bootRun") {
    mainClass = 'com.honeypot.HoneyPotApplication'
    jvmArgs = ["-Dcom.amazonaws.sdk.disableEc2Metadata=true"]
}

tasks.named("bootJar") {
    mainClass = 'com.honeypot.HoneyPotApplication'
}

distributions {
    main {
        contents {
            from 'Dockerfile'
            from 'docker-compose.yml'
            from 'prometheus.yml'
            into('build/libs') {
                from 'build/libs'
            }
            into('keystore') {
                from 'keystore'
            }
            duplicatesStrategy = DuplicatesStrategy.EXCLUDE
        }
    }
}

distTar.enabled = false
distZip.dependsOn('bootJar')